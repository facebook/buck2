# Copyright (c) Meta Platforms, Inc. and affiliates.
#
# This source code is dual-licensed under either the MIT license found in the
# LICENSE-MIT file in the root directory of this source tree or the Apache
# License, Version 2.0 found in the LICENSE-APACHE file in the root directory
# of this source tree. You may select, at your option, one of the
# above-listed licenses.

load("@prelude//:asserts.bzl", "asserts")
load("@prelude//cfg/modifier:common.bzl", "resolve_modifier")
load("@prelude//cfg/modifier:types.bzl", "ModifiersMatchInfo")
load(":util.bxl", "TestRefs", "get_test_refs")

# These tests run `cfg_constructor_pre_constraint_analysis` and `cfg_constructor_post_constraint_analysis` with test arguments
# and check if they return the expected PlatformInfo. They act like unit tests except for the fact that they use certain
# constraint targets from the repo (listed below), which requires running from within fbsource.
# TODO(scottcao): Make this test runnable as isolated test.

def test_resolve_constraint_value_modifier(
        test_refs: TestRefs):
    cfg = test_refs.make_cfg(["ovr_config//os/constraints:macos", "ovr_config//cpu/constraints:arm64"])
    asan_constraint = test_refs.get("ovr_config//build_mode:sanitizer_type[asan]")[ConstraintValueInfo]
    resolved_modifier = resolve_modifier(
        cfg = cfg,
        modifier = asan_constraint,
    )
    asserts.equals(asan_constraint, resolved_modifier)

def test_resolve_modifiers_match_with_constraint_value_keys(
        test_refs: TestRefs):
    modifier = {
        "ovr_config//os/constraints:linux": {
            "DEFAULT": "ovr_config//build_mode:sanitizer_type[no-san]",
            "ovr_config//cpu/constraints:x86_64": "ovr_config//build_mode:sanitizer_type[asan]",
        },
        "ovr_config//os/constraints:windows": {
            # These are fairly contrived match since it only contains DEFAULT
            "DEFAULT": {
                "DEFAULT": {
                    "ovr_config//cpu/constraints:x86_64": "ovr_config//build_mode:sanitizer_type[tsan]",
                },
            },
        },
    }
    modifier_info = test_refs.get_modifier_info(modifier)
    linux_x86_64 = test_refs.make_cfg(["ovr_config//os/constraints:linux", "ovr_config//cpu/constraints:x86_64"])
    linux_arm64 = test_refs.make_cfg(["ovr_config//os/constraints:linux", "ovr_config//cpu/constraints:arm64"])
    macos_x86_64 = test_refs.make_cfg(["ovr_config//os/constraints:macos", "ovr_config//cpu/constraints:x86_64"])
    macos_arm64 = test_refs.make_cfg(["ovr_config//os/constraints:macos", "ovr_config//cpu/constraints:arm64"])
    windows_x86_64 = test_refs.make_cfg(["ovr_config//os/constraints:windows", "ovr_config//cpu/constraints:x86_64"])
    windows_arm64 = test_refs.make_cfg(["ovr_config//os/constraints:windows", "ovr_config//cpu/constraints:arm64"])
    asserts.equals(test_refs.get("ovr_config//build_mode:sanitizer_type[asan]")[ConstraintValueInfo], resolve_modifier(linux_x86_64, modifier_info))
    asserts.equals(test_refs.get("ovr_config//build_mode:sanitizer_type[no-san]")[ConstraintValueInfo], resolve_modifier(linux_arm64, modifier_info))
    asserts.equals(None, resolve_modifier(macos_x86_64, modifier_info))
    asserts.equals(None, resolve_modifier(macos_arm64, modifier_info))
    asserts.equals(test_refs.get("ovr_config//build_mode:sanitizer_type[tsan]")[ConstraintValueInfo], resolve_modifier(windows_x86_64, modifier_info))
    asserts.equals(None, resolve_modifier(windows_arm64, modifier_info))

def test_resolve_modifiers_match_with_config_setting_keys(
        test_refs: TestRefs):
    modifier_info = ModifiersMatchInfo(
        selector = [
            (test_refs.make_cfg(["ovr_config//os/constraints:linux", "ovr_config//cpu/constraints:x86_64"]), test_refs.get("ovr_config//build_mode:sanitizer_type[asan]")[ConstraintValueInfo]),
            (test_refs.make_cfg(["ovr_config//os/constraints:linux", "ovr_config//cpu/constraints:arm64"]), test_refs.get("ovr_config//build_mode:sanitizer_type[tsan]")[ConstraintValueInfo]),
        ],
        default = test_refs.get("ovr_config//build_mode:sanitizer_type[no-san]")[ConstraintValueInfo],
    )
    linux_x86_64 = test_refs.make_cfg(["ovr_config//os/constraints:linux", "ovr_config//cpu/constraints:x86_64"])
    linux_arm64 = test_refs.make_cfg(["ovr_config//os/constraints:linux", "ovr_config//cpu/constraints:arm64"])
    macos_x86_64 = test_refs.make_cfg(["ovr_config//os/constraints:macos", "ovr_config//cpu/constraints:x86_64"])
    macos_arm64 = test_refs.make_cfg(["ovr_config//os/constraints:macos", "ovr_config//cpu/constraints:arm64"])
    windows_x86_64 = test_refs.make_cfg(["ovr_config//os/constraints:windows", "ovr_config//cpu/constraints:x86_64"])
    windows_arm64 = test_refs.make_cfg(["ovr_config//os/constraints:windows", "ovr_config//cpu/constraints:arm64"])
    asserts.equals(test_refs.get("ovr_config//build_mode:sanitizer_type[asan]")[ConstraintValueInfo], resolve_modifier(linux_x86_64, modifier_info))
    asserts.equals(test_refs.get("ovr_config//build_mode:sanitizer_type[tsan]")[ConstraintValueInfo], resolve_modifier(linux_arm64, modifier_info))
    nosan_constraint = test_refs.get("ovr_config//build_mode:sanitizer_type[no-san]")[ConstraintValueInfo]
    asserts.equals(nosan_constraint, resolve_modifier(macos_x86_64, modifier_info), resolve_modifier(macos_x86_64, modifier_info))
    asserts.equals(nosan_constraint, resolve_modifier(macos_arm64, modifier_info))
    asserts.equals(nosan_constraint, resolve_modifier(windows_x86_64, modifier_info))
    asserts.equals(nosan_constraint, resolve_modifier(windows_arm64, modifier_info))

def test_modifiers_match_resolve_first_matching_key(
        test_refs: TestRefs):
    cfg = test_refs.make_cfg(["ovr_config//os/constraints:linux", "ovr_config//cpu/constraints:x86_64"])
    linux_x86_64_match = test_refs.get_modifier_info({
        "ovr_config//cpu/constraints:x86_64": "ovr_config//build_mode:sanitizer_type[tsan]",
        "ovr_config//os/constraints:linux": "ovr_config//build_mode:sanitizer_type[asan]",
    })

    # buildifier: disable=unsorted-dict-items
    x86_64_linux_match = test_refs.get_modifier_info({
        "ovr_config//os/constraints:linux": "ovr_config//build_mode:sanitizer_type[asan]",
        "ovr_config//cpu/constraints:x86_64": "ovr_config//build_mode:sanitizer_type[tsan]",
    })
    asan_constraint = test_refs.get("ovr_config//build_mode:sanitizer_type[asan]")[ConstraintValueInfo]
    tsan_constraint = test_refs.get("ovr_config//build_mode:sanitizer_type[tsan]")[ConstraintValueInfo]
    asserts.equals(tsan_constraint, resolve_modifier(cfg, linux_x86_64_match))
    asserts.equals(asan_constraint, resolve_modifier(cfg, x86_64_linux_match))

def test_modifiers_match_with_none(test_refs: TestRefs):
    match = test_refs.get_modifier_info({
        "DEFAULT": "ovr_config//build_mode:sanitizer_type[tsan]",
        "ovr_config//os/constraints:linux": None,
    })
    cfg = test_refs.make_cfg(["ovr_config//os/constraints:linux"])
    asserts.equals(None, resolve_modifier(cfg, match))
    cfg = test_refs.make_cfg(["ovr_config//os/constraints:windows"])
    asserts.equals(test_refs.get("ovr_config//build_mode:sanitizer_type[tsan]")[ConstraintValueInfo], resolve_modifier(cfg, match))

def test_modifiers_match_with_default_constraint_value(test_refs: TestRefs):
    # Test that a modifier key with a default constraint value matches a platform
    # that doesn't explicitly set that constraint.
    # This tests the fix for the bug where conditional modifiers don't match
    # when the platform relies on the constraint's default value.
    modifier_info = test_refs.get_modifier_info({
        "DEFAULT": None,
        # Key contains os:linux AND sanitizer_type[no-san] (which is the default)
        "ovr_config//os:linux-no-san": "ovr_config//toolchain/clang/constraints:17",
    })

    # Platform with linux AND explicitly set no-san - should match
    linux_with_nosan = test_refs.make_cfg(["ovr_config//os/constraints:linux", "ovr_config//build_mode:sanitizer_type[no-san]"])
    asserts.equals(test_refs.get("ovr_config//toolchain/clang/constraints:17")[ConstraintValueInfo], resolve_modifier(linux_with_nosan, modifier_info))

    # Platform with ONLY linux (sanitizer_type defaults to no-san) - should also match
    linux_without_sanitizer = test_refs.make_cfg(["ovr_config//os/constraints:linux"])
    asserts.equals(test_refs.get("ovr_config//toolchain/clang/constraints:17")[ConstraintValueInfo], resolve_modifier(linux_without_sanitizer, modifier_info))

    # Platform with linux but different sanitizer - should NOT match
    linux_with_asan = test_refs.make_cfg(["ovr_config//os/constraints:linux", "ovr_config//build_mode:sanitizer_type[asan]"])
    asserts.equals(None, resolve_modifier(linux_with_asan, modifier_info))

    # Different OS - should NOT match
    windows_without_sanitizer = test_refs.make_cfg(["ovr_config//os/constraints:windows"])
    asserts.equals(None, resolve_modifier(windows_without_sanitizer, modifier_info))

def test_modifiers_match_with_constraint_without_default(test_refs: TestRefs):
    # Test that when the modifier key has a constraint with no default (default = None),
    # and the platform doesn't set it, the match fails.
    # CPU constraints don't have a default value.
    modifier_info = ModifiersMatchInfo(
        selector = [
            # Key requires both linux AND x86_64 (CPU has no default)
            (test_refs.make_cfg(["ovr_config//os/constraints:linux", "ovr_config//cpu/constraints:x86_64"]), test_refs.get("ovr_config//toolchain/clang/constraints:17")[ConstraintValueInfo]),
        ],
        default = None,
    )

    # Platform with linux AND x86_64 - should match
    linux_x86_64 = test_refs.make_cfg(["ovr_config//os/constraints:linux", "ovr_config//cpu/constraints:x86_64"])
    asserts.equals(test_refs.get("ovr_config//toolchain/clang/constraints:17")[ConstraintValueInfo], resolve_modifier(linux_x86_64, modifier_info))

    # Platform with ONLY linux (no CPU set, and CPU has no default) - should NOT match
    linux_only = test_refs.make_cfg(["ovr_config//os/constraints:linux"])
    asserts.equals(None, resolve_modifier(linux_only, modifier_info))

def test_cell_alias_in_modifier_keys(test_refs: TestRefs):
    # Test that cell aliases work correctly in modifier keys and values.
    # Uses "config//os/constraints:windows" which is an alias for "ovr_config//os/constraints:windows".
    # Uses "config//build dictionary-based key and value mapping correctly handles cell alias resolution.
    match = test_refs.get_modifier_info({
        "DEFAULT": "config//build_mode:sanitizer_type[tsan]",
        "config//os/constraints:windows": None,
    })
    windows_cfg = test_refs.make_cfg(["ovr_config//os/constraints:windows"])
    linux_cfg = test_refs.make_cfg(["ovr_config//os/constraints:linux"])
    asserts.equals(None, resolve_modifier(windows_cfg, match))

    # The result should be the resolved constraint value (ovr_config//...), even though the input used cell alias
    asserts.equals(test_refs.get("ovr_config//build_mode:sanitizer_type[tsan]")[ConstraintValueInfo], resolve_modifier(linux_cfg, match))

def _impl(ctx: bxl.Context):
    test_refs = get_test_refs(ctx)

    test_resolve_modifiers_match_with_constraint_value_keys(test_refs)
    test_resolve_modifiers_match_with_config_setting_keys(test_refs)
    test_modifiers_match_resolve_first_matching_key(test_refs)
    test_modifiers_match_with_none(test_refs)
    test_modifiers_match_with_default_constraint_value(test_refs)
    test_modifiers_match_with_constraint_without_default(test_refs)
    test_cell_alias_in_modifier_keys(test_refs)

test = bxl_main(
    cli_args = {},
    impl = _impl,
)
