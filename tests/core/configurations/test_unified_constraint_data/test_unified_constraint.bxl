# Copyright (c) Meta Platforms, Inc. and affiliates.
#
# This source code is dual-licensed under either the MIT license found in the
# LICENSE-MIT file in the root directory of this source tree or the Apache
# License, Version 2.0 found in the LICENSE-APACHE file in the root directory
# of this source tree. You may select, at your option, one of the
# above-listed licenses.

load(":defs.bzl", "BuildModeInfo", "TransitionBuildModeInfo")

def _assert_eq(a, b):
    if a != b:
        fail("Expected {} == {}".format(a, b))

def _main(ctx: bxl.Context):
    contrait_setting_node = ctx.unconfigured_targets("root//:os")

    _assert_eq(contrait_setting_node.rule_kind, "configuration")

    analysis_res = ctx.analysis(contrait_setting_node)
    default = analysis_res.providers()[DefaultInfo]
    sub_target_keys = set(default.sub_targets.keys())
    _assert_eq(sub_target_keys, set(["linux", "macos", "windows"]))

    cnode = ctx.configured_targets("//:foo_target")
    os = cnode.get_attr("os")
    _assert_eq(os, "linux")
    cpu = cnode.get_attr("cpu")
    _assert_eq(cpu, "x86_64")

    override_modifier_cnode = ctx.configured_targets("//:foo_target", modifiers = ["root//:cpu[arm64]"])
    os = override_modifier_cnode.get_attr("os")
    _assert_eq(os, "linux")
    cpu = override_modifier_cnode.get_attr("cpu")
    _assert_eq(cpu, "arm64")

    _assert_eq(override_modifier_cnode.get_attr("sanitizer_name"), None)

main = bxl_main(
    impl = _main,
    cli_args = {},
)

def _test_cfg_transition(ctx: bxl.Context):
    cnode = ctx.configured_targets("//:force_opt_dep")
    analysis_res = ctx.analysis(cnode)
    build_mode_info = analysis_res.providers()[TransitionBuildModeInfo]
    _assert_eq(build_mode_info.dep, "dev")
    _assert_eq(build_mode_info.mode, "dev")
    _assert_eq(build_mode_info.opt_dep, "opt")

    # target with no transition
    cnode = ctx.configured_targets("//:bar_with_build_mode")
    analysis_res = ctx.analysis(cnode)
    build_mode_info = analysis_res.providers()[BuildModeInfo]
    _assert_eq(build_mode_info.mode, "dev")

test_cfg_transition = bxl_main(
    impl = _test_cfg_transition,
    cli_args = {},
)
